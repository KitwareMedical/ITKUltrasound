# ITK image filters wrapped for ITKUltrasound `itk::CurvilinearArraySpecialCoordinatesImage` inputs.
# Must be wrapped in a separate file after `itkCurvilinearArraySpecialCoordinates.wrap` so that
# SWIG does not implicitly define erroneous overrides for template methods such as
# `TransformPhysicalPointToContinuousIndex`, etc.

# See `WRAPPER_SUBMODULE_ORDER` in CMakeLists.txt for where `itkCASCIFilters.wrap` is set to
# always be wrapped after `itkCurvilinearArraySpecialCoordinatesImage.wrap`.

itk_wrap_include("itkCurvilinearArraySpecialCoordinatesImage.h")
itk_wrap_class("itk::ImageSource" POINTER)
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(real_type ${WRAP_ITK_SCALAR})
      itk_wrap_template("CASCI${ITKM_${real_type}}${d}" "itk::CurvilinearArraySpecialCoordinatesImage<${ITKT_${real_type}},${d}>")
    endforeach()
    foreach(complex_type ${WRAP_ITK_COMPLEX_REAL})
      itk_wrap_template("CASCI${ITKM_${complex_type}}${d}" "itk::CurvilinearArraySpecialCoordinatesImage<${ITKT_${complex_type}},${d}>")
    endforeach()
  endforeach()
itk_end_wrap_class()

itk_wrap_include("itkImage.h")
itk_wrap_class("itk::ImageToImageFilter" POINTER)
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(t ${WRAP_ITK_SCALAR})
      foreach(ut ${WRAP_ITK_INT})
        itk_wrap_template("I${ITKM_${t}}${d}CASCI${ITKM_${ut}}${d}"
          "itk::Image< ${ITKT_${t}}, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${ut}}, ${d} >")
      endforeach()
      foreach(t2 ${WRAP_ITK_SCALAR})
        itk_wrap_template("CASCI${ITKM_${t}}${d}CASCI${ITKM_${t2}}${d}"
          "itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t}}, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t2}}, ${d} >")
      endforeach()
      itk_wrap_template("CASCI${ITKM_${t}}${d}I${ITKM_${t}}${d}"
        "itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t}}, ${d} >, itk::Image< ${ITKT_${t}}, ${d}>")
    endforeach()

    foreach(rt ${WRAP_ITK_REAL})
      foreach(ct ${WRAP_ITK_COMPLEX_REAL})
        itk_wrap_template("CASCI${ITKM_${rt}}${d}CASCI${ITKM_${ct}}${d}"
        "itk::CurvilinearArraySpecialCoordinatesImage<  ${ITKT_${rt}}, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage<  ${ITKT_${ct}}, ${d} >")
        itk_wrap_template("CASCI${ITKM_${ct}}${d}CASCI${ITKM_${rt}}${d}"
        "itk::CurvilinearArraySpecialCoordinatesImage<  ${ITKT_${ct}}, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage<  ${ITKT_${rt}}, ${d} >")
      endforeach()
    endforeach()

    foreach(t3 ${WRAP_ITK_COMPLEX_REAL})
      itk_wrap_template("CASCI${ITKM_${t3}}${d}CASCI${ITKM_${t3}}${d}"
        "itk::CurvilinearArraySpecialCoordinatesImage<  ${ITKT_${t3}}, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage<  ${ITKT_${t3}}, ${d} >")
    endforeach()
  endforeach()
itk_end_wrap_class()

itk_wrap_class("itk::ImageFileReader" POINTER_WITH_SUPERCLASS)
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(t ${WRAP_ITK_SCALAR})
      itk_wrap_template("CASCI${ITKM_${t}}${d}"
        "itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t}}, ${d} >")
    endforeach()
  endforeach()
itk_end_wrap_class()

itk_wrap_class("itk::ImageFileWriter" POINTER)
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(t ${WRAP_ITK_SCALAR})
      itk_wrap_template("CASCI${ITKM_${t}}${d}"
        "itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t}}, ${d} >")
    endforeach()
  endforeach()
itk_end_wrap_class()

itk_wrap_class("itk::CastImageFilter" POINTER_WITH_2_SUPERCLASSES)
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(t1 ${WRAP_ITK_SCALAR})
      foreach(t2 ${WRAP_ITK_SCALAR})
        itk_wrap_template("CASCI${ITKM_${t1}}${d}CASCI${ITKM_${t2}}${d}"
          "itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t1}}, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t2}}, ${d} >")
        itk_wrap_template("CASCI${ITKM_${t1}}${d}I${ITKM_${t2}}${d}"
          "itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t1}}, ${d} >, itk::Image< ${ITKT_${t2}}, ${d} >")
      endforeach()
    endforeach()
  endforeach()
itk_end_wrap_class()

itk_wrap_class("itk::RescaleIntensityImageFilter" POINTER_WITH_2_SUPERCLASSES)
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(t ${WRAP_ITK_SCALAR})
      foreach(ut ${WRAP_ITK_INT})
        itk_wrap_template("I${ITKM_${t}}${d}CASCI${ITKM_${ut}}${d}"
          "itk::Image< ${ITKT_${t}}, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${ut}}, ${d} >")
      endforeach()
    endforeach()
  endforeach()
itk_end_wrap_class()

itk_wrap_include("itkResampleImageFilter.h")
itk_wrap_class("itk::ResampleImageFilter" POINTER)
  itk_wrap_template("CASCIF2IF2"
    "itk::CurvilinearArraySpecialCoordinatesImage< float, 2 >, itk::Image< float, 2 >")
itk_end_wrap_class()

itk_wrap_include("itkSpectra1DSupportWindowToMaskImageFilter.h")
itk_wrap_class("itk::Spectra1DSupportWindowToMaskImageFilter" POINTER_WITH_2_SUPERCLASSES)
  foreach(d ${ITK_WRAP_IMAGE_DIMS})
    foreach(t ${WRAP_ITK_INT})
      itk_wrap_template("IlistitkIndex${d}${d}CASCI${ITKM_${t}}${d}"
        "itk::Image< std::list< itk::Index< ${d} > >, ${d} >, itk::CurvilinearArraySpecialCoordinatesImage< ${ITKT_${t}}, ${d} >")
    endforeach()
  endforeach()
itk_end_wrap_class()
